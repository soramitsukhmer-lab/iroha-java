plugins {
    id 'com.adarshr.test-logger' version '1.6.0'
    id 'maven-publish'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    apply plugin: 'maven'

    sourceCompatibility = 1.8 // java 8
    targetCompatibility = 1.8

    jacoco {
        toolVersion = "0.8.2"
    }

    jacocoTestReport {
        executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

        reports {
            xml.enabled true
            csv.enabled false
            html.enabled false
        }

        dependsOn(test)
    }

    test {
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat "full"
        }
        finalizedBy jacocoTestReport
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10'
}

publishing {
    publications {
        subprojects.each { subproject ->
            "${subproject.name}Jar"(MavenPublication) {
                from subproject.components.java
                groupId = project.properties["group"].toString()
                artifactId = "${subproject.name}"
                version = project.properties["version"].toString()
            }
        }
    }
    repositories {
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/soramitsukhmer-lab/iroha-java'
            credentials {
                credentials {
                    username = project.findProperty("gpr.username") ?: System.getenv("GPR_USERNAME")
                    password = project.findProperty("gpr.password") ?: System.getenv("GPR_PASSWORD")
                }
            }
        }
    }
}
